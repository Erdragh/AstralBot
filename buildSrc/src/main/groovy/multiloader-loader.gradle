plugins {
    id 'multiloader-common'
    id "org.jetbrains.kotlin.jvm"
}

configurations {
    commonJava {
        canBeResolved = true
    }
    commonKotlin {
        canBeResolved = true
    }
    commonResources {
        canBeResolved = true
    }
}

dependencies {
    compileOnly(project(':common')) {
        capabilities {
            requireCapability "$group:$modId"
        }
    }
    commonJava project(path: ':common', configuration: 'commonJava')
    commonKotlin project(path: ":common", configuration: "commonKotlin")
    commonResources project(path: ':common', configuration: 'commonResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

tasks.named("compileKotlin") {
    dependsOn(configurations.commonKotlin)
    source(configurations.commonJava)
    source(configurations.commonKotlin)
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonJava)
    from(configurations.commonJava)
    dependsOn(configurations.commonKotlin)
    from(configurations.commonKotlin)
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}